buildscript{
    repositories {
        mavenLocal()
        jcenter()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.0.0'

    }
}

plugins {
    id 'java'
}
apply plugin: 'java-library'

ext {

    MindustryHash = "v105"
    atomHash = "d2e69aaa4d"

    writeProcessors = {
        new File(rootDir, "CodeGen/src/main/resources/META-INF/services/").mkdirs()
        def processorFile = new File(rootDir, "CodeGen/src/main/resources/META-INF/services/javax.annotation.processing.Processor")
        def text = new StringBuilder()
        def files = new File(rootDir, "CodeGen/src/main/java")
        files.eachFileRecurse(groovy.io.FileType.FILES){ file ->
            if(file.name.endsWith(".java") && (file.text.contains(" extends AtomProcessor") || (file.text.contains(" extends AbstractProcessor") && !file.text.contains("abstract class")))){
                text.append(file.path.substring(files.path.length() + 1)).append("\n")
            }
        }

        processorFile.text = text.toString().replace(".java", "").replace("/", ".").replace("\\", ".")
    }
    localAtom = {
        return new File('../Atoms').exists()
    }

    atom = { String name ->
        if (localAtom()) {
            return project(":Atom:$name")
        } else {
            if (name.contains(':')) name = name.split(':').last()
            return "com.github.o7-Fire.Atomic-Library:$name:$atomHash"
        }
    }
}

repositories {
    mavenCentral()
    maven{ url 'https://www.jitpack.io' }
    flatDir {
        dirs 'libs'
    }
}
task preGen{
    writeProcessors()
}

dependencies {
    compileJava.dependsOn(preGen)
    testImplementation 'junit:junit:4.12'
    compileOnly files('libs/Atomic.jar')
    compileOnly files('libs/Mindustry.jar')
    compileOnly project(":CodeGen")
    annotationProcessor project(":CodeGen")
}


tasks.withType(JavaCompile) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = "UTF-8"
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
jar{
    archiveFileName = "${project.archivesBaseName}.jar"
    from{
        configurations.runtimeClasspath.collect{it.isDirectory() ? it : zipTree(it)}
    }
}
task proguard(dependsOn: jar, type: proguard.gradle.ProGuardTask) {
    configuration 'proguard.txt'
    // Automatically handle the Java version of this build.
    if (System.getProperty('java.version').startsWith('1.')) {
        // Before Java 9, the runtime classes were packaged in a single jar file.
        libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    } else {
        // As of Java 9, the runtime classes are packaged in modular jmod files.
        libraryjars "${System.getProperty('java.home')}/jmods/java.base.jmod", jarfilter: '!**.jar', filter: '!module-info.class'
        //libraryjars "${System.getProperty('java.home')}/jmods/....."
    }
    libraryjars "libs"

    injars "build/libs/${project.archivesBaseName}.jar"
    outjars "build/libs/${project.archivesBaseName}.out.jar"
}
